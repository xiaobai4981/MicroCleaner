plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.leyou.microcleaner'
    compileSdkVersion 36
    defaultConfig {
        applicationId "com.leyou.microcleaner"
        minSdk 24
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        def keystorePropertiesFiles = rootProject.file("keystore.properties")
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFiles))
        basic {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.basic
        }
    }
    buildTypes.configureEach {
        buildConfigField "long", "buildTime", "${System.currentTimeMillis()}"
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def type = variant.buildType.name
            def releaseApkName = rootProject.name + '_' + type + "_" + versionName + '.apk'
            outputFileName = releaseApkName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation("androidx.core:core-ktx:1.6.0")
    implementation("androidx.appcompat:appcompat:1.3.1")
    implementation("com.google.android.material:material:1.4.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.0")
    implementation("com.github.bonepeople:AndroidBase:1.7.0")
    implementation 'androidx.activity:activity:1.8.0'

    // Coil https://github.com/coil-kt/coil
    def coilVersion = "2.1.0"
    implementation("io.coil-kt:coil:$coilVersion")
    // Gif extension library
    implementation("io.coil-kt:coil-gif:$coilVersion")
    // To add video frame support, import the extension library:
    implementation("io.coil-kt:coil-video:$coilVersion")
}
